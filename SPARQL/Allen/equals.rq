PREFIX i: <https://www.imi.med.fau.de/AllenSparql/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT { 

  ?interval1 i:equals ?interval2 .
    
} WHERE {
  
  # must be the same patient:
  ?interval1 i:hasPatient ?pat .
  ?interval2 i:hasPatient ?pat .
  
  # Must have Unix time stamps (for computation)
  ?interval1 i:hasStartDateUnix ?startdate1_ux .
  ?interval1 i:hasEndDateUnix ?enddate1_ux .
  ?interval2 i:hasStartDateUnix ?startdate2_ux .
  ?interval2 i:hasEndDateUnix ?enddate2_ux .
      
  # temporal filtering:
  FILTER (?startdate2_ux >= ?startdate1_ux - #UNCERTAINTY# &&
          ?startdate2_ux <= ?startdate1_ux + #UNCERTAINTY# &&
	      ?enddate2_ux <= ?enddate1_ux + #UNCERTAINTY# &&
          ?enddate2_ux >= ?enddate1_ux - #UNCERTAINTY#) .  
  
  FILTER ( ?interval1 != ?interval2 ) .
  
  # remove all intervals that have already been processed:
  FILTER NOT EXISTS { ?interval1 i:isMarked "aggregated" }
  FILTER NOT EXISTS { ?interval2 i:isMarked "aggregated" }

}